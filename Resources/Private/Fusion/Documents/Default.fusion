prototype(Neos.Neos:Page) {
    # Don't render lastVisitedNodeScript in live workspace as we usually don't need it
    lastVisitedNodeScript.@if.notLive = ${node.context.workspace.name != 'live'}

    htmlTag {
        attributes {
            class = 'no-js'
        }
    }

    head {
        viewportTag = Shel.NeosBase:MetaTag {
            attributes {
                name = 'viewport'
                content = 'width=device-width, initial-scale=1'
            }
        }

        ieCompatibilityTag = Shel.NeosBase:MetaTag {
            attributes {
                http-equiv = 'X-UA-Compatible'
                content = 'IE=edge'
            }
            @position = 'start'
        }

        authorTag = Shel.NeosBase:MetaTag {
            authorName = ''
            @context.authorName = ${this.authorName}
            attributes {
                name = 'author'
                content = ${authorName}
            }
            @if.isNotBlank = ${!String.isBlank(authorName)}
        }

        # Override the href attribute of the apple touch icon tag in your site package
        appleTouchIconTag = Shel.NeosBase:LinkTag {
            attributes.rel = 'apple-touch-icon'
        }

        # Override the href attribute of the favicon tag in your site package
        faviconTag = Shel.NeosBase:LinkTag {
            attributes {
                rel = 'shortcut icon'
                type = 'image/x-icon'
            }
        }

        stylesheets {
            # Override the href attribute of the main styles tag in your site package
            main = Shel.NeosBase:StyleSheetTag
        }

        javascripts {
            # Only add absolutely necessary tags here from your site package
        }
    }

    body {
        javascripts {
            # Override the src attribute of your modernizr script in your site package
            modernizr = Shel.NeosBase:ScriptTag

            # Override the src attribute of your javascripts bundle file in your site package
            bundle = Shel.NeosBase:ScriptTag {
                async = true
            }
        }
    }

    liveReload = Neos.Fusion:Tag {
        tagName = 'script'
        content = ${'document.write(\'<script async src="http://\' + (location.host || \'localhost\').split(\':\')[0] + \':' + Configuration.setting('Shel.NeosBase.debug.liveReload.port') + '/livereload.js?snipver=1"></\' + \'script>\')'}
        @position = 'before closingBodyTag'
        @if {
            inLiveWorkspace = ${node.context.workspaceName == 'live'}
            isEnabled = ${Configuration.setting('Shel.NeosBase.debug.liveReload.enabled')}
        }
    }
    browserSync = Neos.Fusion:Tag {
        tagName = 'script'
        attributes.id = '__bs_script__'
        content = ${'document.write("<script async src=\'//HOST:' + Configuration.setting('Shel.NeosBase.debug.browserSync.port') + '/browser-sync/browser-sync-client.js?v=2.19.0\'><\/script>".replace("HOST", location.hostname));'}
        @position = 'before closingBodyTag'
        @if {
            inLiveWorkspace = ${node.context.workspaceName == 'live'}
            isEnabled = ${Configuration.setting('Shel.NeosBase.debug.browserSync.enabled')}
        }
    }
}
